plugins {
  id 'base'
  id "com.github.node-gradle.node" version "7.1.0"
}

group 'run.halo.starter.ui'

tasks.register('buildFrontend', PnpmTask) {
  args = ['build']
  dependsOn tasks.named('pnpmInstall')
  inputs.dir(layout.projectDirectory.dir('src'))
  inputs.files(fileTree(
    dir: layout.projectDirectory,
    includes: ['*.cjs', '*.ts', '*.js', '*.json', '*.yaml']))
  outputs.dir(layout.buildDirectory.dir('dist'))
  shouldRunAfter(tasks.named('check'))
}

tasks.register('checkFrontend', PnpmTask) {
  args = ['test:unit']
  dependsOn tasks.named('pnpmInstall')
}

tasks.named('check') {
  dependsOn tasks.named('checkFrontend')
}

tasks.named('build') {
  dependsOn tasks.named('buildFrontend')
}
